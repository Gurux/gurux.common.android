plugins {
    alias(libs.plugins.androidApplication)
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'gurux.common'
    compileSdk 34

    defaultConfig {
        minSdk 31
        targetSdk 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "org.gurux"
                artifactId = "Gurux.Common.Android"
                version = "2.0.2"

                signing {
                    sign publishing.publications.release
                }

                pom {
                    name = 'gurux.common.android'
                    description = 'gurux.common.android package implements interfaces that are needed for Gurux Media components and Gurux Device Framework. Purpose of Gurux Device Framework is help you to read your devices, meters and sensors easier'
                    url = 'https://www.gurux.fi'
                    licenses {
                        license {
                            name = 'GNU General Public License, version 2'
                            url = 'http://www.gnu.org/licenses/gpl-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'gurux'
                            name = 'Gurux ltd'
                            email = 'gurux@gurux.fi'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/gurux/gurux.common.android.git'
                        developerConnection = 'scm:git:https://github.com/gurux/gurux.common.android.git'
                        url = 'https://github.com/gurux/gurux.common.android'
                    }
                }
                boolean isEnabled = project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')
                repositories {
                    maven {
                        name = "Maven central"
                        url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
                        if (isEnabled){
                            credentials{
                                username = sonatypeUsername
                                password = sonatypePassword
                            }
                        }
                    }
                }
            }
        }
    }
}